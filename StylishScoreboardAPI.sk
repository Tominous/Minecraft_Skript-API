function stylishScoreboardAPI_newScoreboard(player: player, ID: text, slots: number = 15):
	delete stylish scoreboard "%{_ID}%.%{_player}%"
	create new stylish scoreboard named "%{_ID}%.%{_player}%"
	loop {_slots} times:
		create a new id based score "%{_ID}%.%{_player}%.slot-%(16 - loop-number)%" with text "&fNone (##%(16 - loop-number)%)" slot (16 - loop-number) for stylish scoreboard "%{_ID}%.%{_player}%"
	set stylish scoreboard of {_player} to "%{_ID}%.%{_player}%"
	
function stylishScoreboardAPI_removeScoreboard(player: player, ID: text):
	delete stylish scoreboard "%{_ID}%.%{_player}%"

function stylishScoreboardAPI_editScoreboardTitle(player: player, ID: text, newText: text):
	set title of stylish scoreboard "%{_ID}%.%{_player}%" to "%{_newText}%"

function stylishScoreboardAPI_editScoreboard(player: player, ID: text, slotID: number, newText: text):
	set text of id "%{_ID}%.%{_player}%.slot-%{_slotID}%" to "%{_newText}%"
	
function stylishScoreboardAPI_getTimingDisplay(countdown: number) :: text:
	set {_minutes} to floor({_countdown} / 60)
	set {_seconds} to ({_countdown} - ({_minutes} * 60))
	if {_minutes} < 10:
		set {_minutesDisplay} to "0%{_minutes}%"
	else:
		set {_minutesDisplay} to "%{_minutes}%"
	if {_seconds} < 10:
		set {_secondsDisplay} to "0%{_seconds}%"
	else:
		set {_secondsDisplay} to "%{_seconds}%"
	return "%{_minutesDisplay}%:%{_secondsDisplay}%"